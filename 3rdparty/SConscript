import os.path

Import('env', 'subenvs', 'meta')

# default versions for downloaded third-party libraries
thirdparty_versions = {
    'libuv':            '1.35.0',
    'libatomic_ops':    '7.6.10',
    'libunwind':        '1.2.1',
    'openfec':          '1.4.2.4',
    'speexdsp':         '1.2.0',
    'sox':              '14.4.2',
    'alsa':             '1.0.29',
    'pulseaudio':       '5.0',
    'json-c':           '0.12-20140410',
    'ltdl':             '2.4.6',
    'sndfile':          '1.0.28',
    'ragel':            '6.10',
    'gengetopt':        '2.22.6',
    'cpputest':         '3.6',
    'google-benchmark': '1.5.0',
}

# root directory for building third-parties
thirdparty_build_dir = env['ROC_THIRDPARTY_BUILDDIR']

# check if local platform differs from target platform
is_crosscompiling = (meta.build != meta.host)

# source code of these dependencies is included directly in our repo
vendored_dependencies = ['hedley']

# all dependencies that should be either installed system-wide or downloaded by us
external_dependencies = set([t.replace('target_', '') for t in env['ROC_TARGETS']])

# on macos libunwind is provided by the OS
if meta.platform in ['darwin']:
    external_dependencies.discard('libunwind')

external_dependencies.add('ragel')

if not GetOption('disable_tools'):
    external_dependencies.add('gengetopt')

if GetOption('enable_pulseaudio_modules'):
    external_dependencies.add('pulseaudio')

if 'pulseaudio' in external_dependencies and meta.platform in ['linux']:
    external_dependencies.add('alsa')

if GetOption('enable_tests'):
    external_dependencies.add('cpputest')

if GetOption('enable_benchmarks'):
    external_dependencies.add('google-benchmark')

# dependencies that we should download and build manually
download_dependencies = set()

# dependencies that have explicitly provided version
explicit_version = set()

for name, version in env.ParseThirdParties(GetOption('build_3rdparty')):
    if name != 'all' and not name in thirdparty_versions:
        env.Die("unknown thirdparty name '%s' in '--build-3rdparty', expected any of: %s",
                    name, ', '.join(['all'] + list(sorted(thirdparty_versions.keys()))))
    download_dependencies.add(name)
    if version:
        thirdparty_versions[name] = version
        explicit_version.add(name)

if 'all' in download_dependencies:
    download_dependencies = external_dependencies

# dependencies that should be pre-installed on system
system_dependencies = external_dependencies - download_dependencies

if 'hedley' in vendored_dependencies:
    env.Append(CPPPATH=['#3rdparty/vendor/hedley'])

if 'libuv' in system_dependencies:
    conf = Configure(env, custom_tests=env.CustomTests)

    if not conf.AddPkgConfigDependency('libuv', '--cflags --libs'):
        conf.env.AddPkgConfigLibs(['uv'])

    if not is_crosscompiling:
        if not conf.CheckLibWithHeaderExt(
            'uv', 'uv.h', 'C', expr='UV_VERSION_MAJOR >= 1 && UV_VERSION_MINOR >= 5'):
            env.Die("libuv >= 1.5 not found (see 'config.log' for details)")
    else:
        if not conf.CheckLibWithHeaderExt('uv', 'uv.h', 'C', run=False):
            env.Die("libuv not found (see 'config.log' for details)")

    env = conf.Finish()

if 'libunwind' in system_dependencies:
    conf = Configure(env, custom_tests=env.CustomTests)

    if not conf.AddPkgConfigDependency('libunwind', '--cflags --libs'):
        conf.env.AddPkgConfigLibs(['unwind'])

    if not conf.CheckLibWithHeaderExt('unwind', 'libunwind.h', 'C', run=not is_crosscompiling):
        env.Die("libunwind not found (see 'config.log' for details)")

    env = conf.Finish()

if 'libatomic_ops' in system_dependencies:
    conf = Configure(env, custom_tests=env.CustomTests)

    if not conf.AddPkgConfigDependency('atomic_ops', '--cflags --libs'):
        conf.env.AddPkgConfigLibs(['atomic_ops'])

    if not conf.CheckLibWithHeaderExt('atomic_ops', 'atomic_ops.h', 'C',
                                      run=not is_crosscompiling):
        env.Die("libatomic_ops not found (see 'config.log' for details)")

    env = conf.Finish()

if 'openfec' in system_dependencies:
    conf = Configure(env, custom_tests=env.CustomTests)

    if not conf.AddPkgConfigDependency('openfec', '--silence-errors --cflags --libs'):
        conf.env.AddPkgConfigLibs(['openfec'])

        if GetOption('with_openfec_includes'):
            openfec_includes = GetOption('with_openfec_includes')
            conf.env.Append(CPPPATH=[
                openfec_includes,
                '%s/lib_common' % openfec_includes,
                '%s/lib_stable' % openfec_includes,
            ])
        elif not is_crosscompiling:
           for prefix in ['/usr/local', '/usr']:
               if os.path.exists('%s/include/openfec' % prefix):
                   conf.env.Append(CPPPATH=[
                       '%s/include/openfec' % prefix,
                       '%s/include/openfec/lib_common' % prefix,
                       '%s/include/openfec/lib_stable' % prefix,
                   ])
                   conf.env.Append(LIBPATH=[
                       '%s/lib' % prefix,
                   ])
                   break

    if not conf.CheckLibWithHeaderExt(
            'openfec', 'of_openfec_api.h', 'C', run=not is_crosscompiling):
        env.Die("openfec not found (see 'config.log' for details)")

    if not conf.CheckDeclaration('OF_USE_ENCODER', '#include <of_openfec_api.h>', 'c'):
        env.Die("openfec has no encoder support (OF_USE_ENCODER)")

    if not conf.CheckDeclaration('OF_USE_DECODER', '#include <of_openfec_api.h>', 'c'):
        env.Die("openfec has no encoder support (OF_USE_DECODER)")

    if not conf.CheckDeclaration('OF_USE_LDPC_STAIRCASE_CODEC',
                                 '#include <of_openfec_api.h>', 'c'):
        env.Die(
            "openfec has no LDPC-Staircase codec support (OF_USE_LDPC_STAIRCASE_CODEC)")

    env = conf.Finish()

if 'speexdsp' in system_dependencies:
    conf = Configure(env, custom_tests=env.CustomTests)

    if not conf.AddPkgConfigDependency('speexdsp', '--cflags --libs'):
        conf.env.AddPkgConfigLibs(['speexdsp'])

    if not conf.CheckLibWithHeaderExt('speexdsp', 'speex/speex_resampler.h', 'C',
                                          run=not is_crosscompiling):
        env.Die("speexdsp not found (see 'config.log' for details)")

    env = conf.Finish()

if 'pulseaudio' in system_dependencies:
    conf = Configure(subenvs.tools, custom_tests=env.CustomTests)

    if not conf.AddPkgConfigDependency('libpulse', '--cflags --libs'):
        conf.env.AddPkgConfigLibs(['pulse'])

    if not conf.CheckLibWithHeaderExt(
            'pulse', 'pulse/pulseaudio.h', 'C', run=not is_crosscompiling):
        env.Die("libpulse not found (see 'config.log' for details)")

    subenvs.tools = conf.Finish()

    if GetOption('enable_examples'):
        conf = Configure(subenvs.examples, custom_tests=env.CustomTests)

        if not conf.AddPkgConfigDependency('libpulse-simple', '--cflags --libs'):
            conf.env.AddPkgConfigLibs(['pulse-simple'])

        if not conf.CheckLibWithHeaderExt(
                'pulse-simple', 'pulse/simple.h', 'C', run=not is_crosscompiling):
            env.Die("libpulse-simple not found (see 'config.log' for details)")

        subenvs.examples = conf.Finish()

    if GetOption('enable_pulseaudio_modules'):
        conf = Configure(subenvs.pulse, custom_tests=env.CustomTests)

        if not conf.CheckLibWithHeaderExt('ltdl', 'ltdl.h', 'C', run=not is_crosscompiling):
            env.Die("ltdl not found (see 'config.log' for details)")

        subenvs.pulse = conf.Finish()

        pa_src_dir = GetOption('with_pulseaudio')
        if not pa_src_dir:
            env.Die('--enable-pulseaudio-modules requires either --with-pulseaudio'+
                    ' or --build-3rdparty=pulseaudio')

        pa_build_dir = GetOption('with_pulseaudio_build_dir')
        if not pa_build_dir:
            pa_build_dir = pa_src_dir

        subenvs.pulse.Append(CPPPATH=[
            pa_build_dir,
            pa_src_dir + '/src',
        ])

        for lib in ['libpulsecore-*.so', 'libpulsecommon-*.so']:
            path = '%s/src/.libs/%s' % (pa_build_dir, lib)
            libs = env.Glob(path)
            if not libs:
                env.Die("can't find %s" % path)

            subenvs.pulse.AddPkgConfigLibs(libs)

            m = re.search('-([0-9.]+).so$', libs[0].path)
            if m:
                pa_ver = m.group(1)

        if not pa_ver:
            env.Die("can't determine pulseaudio version")

        env['ROC_PULSE_VERSION'] = pa_ver

if 'sox' in system_dependencies:
    conf = Configure(subenvs.tools, custom_tests=env.CustomTests)

    if not conf.AddPkgConfigDependency('sox', '--cflags --libs'):
        conf.env.AddPkgConfigLibs(['sox'])

    if not is_crosscompiling:
        if not conf.CheckLibWithHeaderExt(
                'sox', 'sox.h', 'C',
                expr='SOX_LIB_VERSION_CODE >= SOX_LIB_VERSION(14, 4, 0)'):
            env.Die("libsox >= 14.4.0 not found (see 'config.log' for details)")
    else:
        if not conf.CheckLibWithHeaderExt('sox', 'sox.h', 'C', run=False):
            env.Die("libsox not found (see 'config.log' for details)")

    subenvs.tools = conf.Finish()

if 'ragel' in system_dependencies:
    conf = Configure(env, custom_tests=env.CustomTests)

    if 'RAGEL' in env.Dictionary():
        ragel = env['RAGEL']
    else:
        ragel = 'ragel'

    if not conf.CheckProg(ragel):
        env.Die("ragel not found in PATH (looked for '%s')" % ragel)

    env = conf.Finish()

if 'gengetopt' in system_dependencies:
    conf = Configure(env, custom_tests=env.CustomTests)

    if 'GENGETOPT' in env.Dictionary():
        gengetopt = env['GENGETOPT']
    else:
        gengetopt = 'gengetopt'

    if not conf.CheckProg(gengetopt):
        env.Die("gengetopt not found in PATH (looked for '%s')" % gengetopt)

    env = conf.Finish()

if 'cpputest' in system_dependencies:
    conf = Configure(subenvs.tests, custom_tests=env.CustomTests)

    if not conf.AddPkgConfigDependency('cpputest', '--cflags --libs'):
        conf.env.AddPkgConfigLibs(['CppUTest'])

    if not conf.CheckLibWithHeaderExt(
            'CppUTest', 'CppUTest/TestHarness.h', 'CXX', run=not is_crosscompiling):
        subenvs.tests.Die("CppUTest not found (see 'config.log' for details)")

    subenvs.tests = conf.Finish()

if 'google-benchmark' in system_dependencies:
    conf = Configure(subenvs.tests, custom_tests=env.CustomTests)

    if not conf.AddPkgConfigDependency('benchmark', '--silence-errors --cflags --libs'):
        conf.env.AddPkgConfigLibs(['benchmark'])

    if not conf.CheckLibWithHeaderExt(
            'benchmark', 'benchmark/benchmark.h', 'CXX', run=not is_crosscompiling):
        subenvs.tests.Die("Google Benchmark not found (see 'config.log' for details)")

    subenvs.tests = conf.Finish()

if 'libuv' in download_dependencies:
    env.ThirdParty(thirdparty_build_dir, meta.toolchain,
                   meta.thirdparty_variant, thirdparty_versions, 'libuv')

if 'libunwind' in download_dependencies:
    env.ThirdParty(thirdparty_build_dir,
                   meta.toolchain, meta.thirdparty_variant,
                   thirdparty_versions, 'libunwind')

if 'libatomic_ops' in download_dependencies:
    env.ThirdParty(thirdparty_build_dir,
                   meta.toolchain, meta.thirdparty_variant,
                   thirdparty_versions, 'libatomic_ops')

if 'openfec' in download_dependencies:
    env.ThirdParty(thirdparty_build_dir, meta.toolchain,
                   meta.thirdparty_variant, thirdparty_versions,
                   'openfec', includes=[
                        'lib_common',
                        'lib_stable',
                        ])

if 'speexdsp' in download_dependencies:
    env.ThirdParty(thirdparty_build_dir, meta.toolchain,
                meta.thirdparty_variant, thirdparty_versions, 'speexdsp')

if 'alsa' in download_dependencies:
    subenvs.tools.ThirdParty(thirdparty_build_dir, meta.toolchain,
                        meta.thirdparty_variant, thirdparty_versions, 'alsa')

if 'pulseaudio' in download_dependencies:
    if not 'pulseaudio' in explicit_version and not is_crosscompiling:
        pa_ver = env.ParseToolVersion('pulseaudio --version')
        if pa_ver:
            thirdparty_versions['pulseaudio'] = pa_ver

    pa_deps = [
        'ltdl',
        'json-c',
        'sndfile',
        ]

    if 'alsa' in download_dependencies:
        pa_deps += ['alsa']

    env['ROC_PULSE_VERSION'] = thirdparty_versions['pulseaudio']

    subenvs.tools.ThirdParty(thirdparty_build_dir, meta.toolchain,
                        meta.thirdparty_variant, thirdparty_versions, 'ltdl')
    subenvs.tools.ThirdParty(thirdparty_build_dir, meta.toolchain,
                        meta.thirdparty_variant, thirdparty_versions, 'json-c')
    subenvs.tools.ThirdParty(thirdparty_build_dir, meta.toolchain,
                        meta.thirdparty_variant, thirdparty_versions, 'sndfile')
    subenvs.tools.ThirdParty(thirdparty_build_dir, meta.toolchain,
                        meta.thirdparty_variant, thirdparty_versions,
                        'pulseaudio', deps=pa_deps, libs=['pulse', 'pulse-simple'])

    subenvs.examples.ImportThridParty(thirdparty_build_dir, meta.toolchain,
                                 thirdparty_versions, 'ltdl')
    subenvs.examples.ImportThridParty(thirdparty_build_dir, meta.toolchain,
                                 thirdparty_versions, 'pulseaudio',
                                 libs=['pulse', 'pulse-simple'])

    pa_ver_short = '.'.join(thirdparty_versions['pulseaudio'].split('.')[:2])

    subenvs.pulse.ImportThridParty(thirdparty_build_dir, meta.toolchain,
                               thirdparty_versions, 'ltdl')
    subenvs.pulse.ImportThridParty(thirdparty_build_dir, meta.toolchain,
                               thirdparty_versions, 'pulseaudio',
                               libs=[
                                   'pulsecore-%s' % pa_ver_short,
                                   'pulsecommon-%s' % pa_ver_short
                                   ])

if 'sox' in download_dependencies:
    sox_deps = []

    if 'alsa' in download_dependencies:
        sox_deps += ['alsa']

    if 'pulseaudio' in download_dependencies:
        sox_deps += ['pulseaudio']

    subenvs.tools.ThirdParty(thirdparty_build_dir, meta.toolchain,
                        meta.thirdparty_variant, thirdparty_versions, 'sox', sox_deps)

    conf = Configure(subenvs.tools, custom_tests=env.CustomTests)

    for lib in [
            'z', 'magic',
            'gsm', 'FLAC',
            'vorbis', 'vorbisenc', 'vorbisfile', 'ogg',
            'mad', 'mp3lame']:
        conf.CheckLib(lib)

    if not 'alsa' in download_dependencies:
        for lib in [
                'asound',
                ]:
            conf.CheckLib(lib)

    if not 'pulseaudio' in download_dependencies:
        for lib in [
                'sndfile',
                'pulse', 'pulse-simple',
                ]:
            conf.CheckLib(lib)

    if meta.platform in ['darwin']:
        subenvs.tools.Append(LINKFLAGS=[
            '-Wl,-framework,CoreAudio'
        ])

    subenvs.tools = conf.Finish()

if 'ragel' in download_dependencies:
    env.ThirdParty(thirdparty_build_dir, "",
                   meta.thirdparty_variant, thirdparty_versions, 'ragel')

    subenvs.generated_code['RAGEL'] = env.File(
        '%s/build/ragel-%s/bin/ragel%s' % (
            thirdparty_build_dir,
            thirdparty_versions['ragel'],
            env['PROGSUFFIX']))

if 'gengetopt' in download_dependencies:
    env.ThirdParty(thirdparty_build_dir, "",
                   meta.thirdparty_variant, thirdparty_versions, 'gengetopt')

    subenvs.generated_code['GENGETOPT'] = env.File(
        '%s/build/gengetopt-%s/bin/gengetopt%s' % (
            thirdparty_build_dir,
            thirdparty_versions['gengetopt'],
            env['PROGSUFFIX']))

if 'cpputest' in download_dependencies:
    subenvs.tests.ThirdParty(thirdparty_build_dir, meta.toolchain,
                        meta.thirdparty_variant, thirdparty_versions, 'cpputest')

if 'google-benchmark' in download_dependencies:
    subenvs.tests.ThirdParty(thirdparty_build_dir, meta.toolchain,
                        meta.thirdparty_variant, thirdparty_versions, 'google-benchmark')

conf = Configure(env, custom_tests=env.CustomTests)

if GetOption('enable_pulseaudio_modules'):
    if GetOption('pulseaudio_module_dir'):
        conf.env['ROC_PULSE_MODULEDIR'] = GetOption('pulseaudio_module_dir')
    else:
        conf.FindPulseDir(
            GetOption('prefix'), meta.build, meta.host, env['ROC_PULSE_VERSION'])

env = conf.Finish()

return_value = (env, subenvs)
Return('return_value')
